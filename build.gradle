plugins {
    id 'java'
    id 'application'
    id 'com.github.gmazzo.buildconfig' version '3.1.0'
}

group 'com.shade'
version '0.1.8'

repositories {
    mavenCentral()
}

def getCommitHash() {
    def stdout = new ByteArrayOutputStream()
    try {
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
            ignoreExitValue true
        }
    } catch (Exception ignored) {
        // no git installed?
    }
    if (stdout.size() == 0) {
        stdout << 'master'
    }
    return stdout.toString().trim()
}

buildConfig {
    buildConfigField('String', 'APP_TITLE', "\"Decima Workshop\"")
    buildConfigField('String', 'APP_VERSION', "\"${project.version}\"")
    buildConfigField('String', 'BUILD_COMMIT', "\"${getCommitHash()}\"")
    buildConfigField('long', 'BUILD_TIME', "${System.currentTimeMillis()}L")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'com.shade.decima.ui.Application'
    applicationDefaultJvmArgs = [
            // Required for UIUtils#minimizePanel
            '--add-opens', 'java.desktop/javax.swing.plaf.basic=ALL-UNNAMED'
    ]
}

distributions {
    main {
        contents {
            from('data') {
                include '**/*.gz'
                into 'data'
            }
        }
    }
}

dependencies {
    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.12.1'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10'

    // https://mvnrepository.com/artifact/org.reflections/reflections
    implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.4'

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    implementation group: 'com.miglayout', name: 'miglayout-swing', version: '11.0'

    // https://mvnrepository.com/artifact/com.formdev
    implementation group: 'com.formdev', name: 'flatlaf', version: '2.6'
    implementation group: 'com.formdev', name: 'flatlaf-extras', version: '2.6'

    // https://mvnrepository.com/artifact/info.picocli/picocli
    implementation group: 'info.picocli', name: 'picocli', version: '4.7.0'

    // https://mvnrepository.com/artifact/org.junit.jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.1'
}
